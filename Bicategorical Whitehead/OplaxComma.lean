/-
Copyright (c) 2025 Judah Towery. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Judah Towery
-/

import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Lax
import Mathlib.CategoryTheory.Bicategory.NaturalTransformation.Oplax
import Mathlib.CategoryTheory.Bicategory.Functor.StrictPseudofunctor

/-!

# The oplax comma bicategory for a lax functor `F : A â¥¤á´¸ T` and an oplax functor `G : B â¥¤áµ’áµ–á´¸ T`.

* objects are triples `(a : A, b : B, Ï† : Fa âŸ¶ Gb)`
* 1-cells are triples `(p : aâ‚€ âŸ¶ aâ‚, q : bâ‚€ âŸ¶ bâ‚, Î¸ : Gq Ï†â‚€ âŸ¶ Ï†â‚ Fp)`
* 2-cells are pairs `(Î± : p âŸ¶ p', Î² : q âŸ¶ q')` that satisfy the generalized ice cream 
* cone condition.

Provides as well change-of-leg strict pseudofunctors. 
For lax functors `F H : A â¥¤á´¸ T` with a lax natural transformation `Î· : H âŸ¶ F`, 
and oplax `G : B â¥¤áµ’áµ–á´¸ T`,  the change-of-left-leg strict pseudofunctor `Comma F G â¥¤áµ– Comma H G` 
is given

* on objects by `(a, b, Ï†) â†¦ (a, b, Ï†Î·(a))`
* on 1-cells by `(p, q, Î¸) â†¦ (p, q, Î¸')`,
* where `Î¸'` is given by the composite
* `Gq(Ï†â‚€Î·(aâ‚€)) âŸ¶ (Gq Ï†â‚€)Î·(aâ‚€) âŸ¶ (Ï†â‚Fp)Î·(aâ‚€) âŸ¶ Ï†â‚(Fp Î·(aâ‚€)) âŸ¶ Ï†â‚(Î·(aâ‚)Hp) âŸ¶ (Ï†â‚Î·(aâ‚))Hp`
* on 2-cells by `(Î±, Î²) â†¦ (Î±, Î²)`.

For a lax functor `F : A â¥¤á´¸ T` and oplax `G H : B â¥¤áµ’áµ–á´¸ T` with a lax natural transformation 
`Î· : G âŸ¶ H`, the change-of-right-leg strict pseudofunctor `Comma F G â¥¤áµ– Comma F H` is given

* on objects by `(a, b, Ï† : Fa âŸ¶ Gb) â†¦ (a, b, Î·(b)Ï†)`
* on 1-cells by `(p, q, Î¸) â†¦ (p, q, Î¸')`,
* where `Î¸'` is given by the composite
* `Hq(Î·(bâ‚€)Ï†â‚€) âŸ¶ (Hq Î·(bâ‚€))Ï†â‚€ âŸ¶ (Î·(bâ‚)Gp)Ï†â‚€ âŸ¶ Î·(bâ‚)(Gp Ï†â‚€) âŸ¶ Î·(bâ‚)(Ï†â‚Fp) âŸ¶ (Î·(bâ‚)Ï†â‚)Fp`
* on 2-cells by `(Î±, Î²) â†¦ (Î±, Î²)`.

-/

namespace CategoryTheory.Bicategory

open Category Bicategory

universe wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ

variable {A B T : Type*} [Bicategory.{wâ‚, vâ‚} A] [Bicategory.{wâ‚‚, vâ‚‚} B] [Bicategory.{wâ‚ƒ, vâ‚ƒ} T]

/-- Objects. -/
@[ext]
structure Comma (F : A â¥¤á´¸ T) (G : B â¥¤áµ’áµ–á´¸ T) where
  left : A
  right : B
  hom : F.obj left âŸ¶ G.obj right

namespace Comma

variable {F : A â¥¤á´¸ T} {G : B â¥¤áµ’áµ–á´¸ T}

/-- 1-cells. -/
@[ext]
structure Homâ‚ (X Y : Comma F G) where
  left : X.left âŸ¶ Y.left
  right : X.right âŸ¶ Y.right
  f : X.hom â‰« G.map right âŸ¶ F.map left â‰« Y.hom

/-- For an object `(a, b, Ï†)`, the identity 1-cell is
`(ğŸ™a, ğŸ™b, Î¸â‚)` where `Î¸â‚ : G(ğŸ™b) Ï† âŸ¶ Ï† F(ğŸ™a)` is given by the canonical composite
`G(ğŸ™b) Ï† âŸ¶ ğŸ™(Gb) Ï† âŸ¶ Ï† âŸ¶ Ï† ğŸ™(Fa) âŸ¶ Ï† F(ğŸ™a)`. -/
@[simps]
def idâ‚ (X : Comma F G) : Homâ‚ X X where
  left := ğŸ™ X.left
  right := ğŸ™ X.right
  f := X.hom â— (G.mapId X.right) â‰« (Ï_ X.hom).hom â‰« (Î»_ X.hom).inv â‰« (F.mapId X.left) â–· X.hom

/-- For two 1-cells `(p, q, Î¸) : (aâ‚€, bâ‚€, Ï†â‚€) âŸ¶ (aâ‚, bâ‚, Ï†â‚)`,
`(p', q', Î¸') : (aâ‚, bâ‚, Ï†â‚) âŸ¶ (aâ‚‚, bâ‚‚, Ï†â‚‚)`, the composition `(p', q', Î¸) âˆ˜ (p, q, Î¸)`
is given by `(p'p, q'q, Î¸'')`, where `Î¸'' : G(q'q)Ï†â‚€ âŸ¶ Ï†â‚‚F(p'p)` is given by the canonical composite
`G(q'q)Ï†â‚€ âŸ¶ (Gq'Gq)Ï†â‚€ âŸ¶ Gq'(GqÏ†â‚€) âŸ¶ Gq'(Ï†â‚Fp) âŸ¶ (Gq'Ï†â‚)Fp âŸ¶ (Ï†â‚‚Fp')Fp âŸ¶ Ï†â‚‚(Fp'Fp) âŸ¶ Ï†â‚‚(Fp'p)`. -/
@[simps]
def compâ‚ {X Y Z : Comma F G} (P : Homâ‚ X Y) (Q : Homâ‚ Y Z) : Homâ‚ X Z where
  left := P.left â‰« Q.left
  right := P.right â‰« Q.right
  f := X.hom â— (G.mapComp P.right Q.right) â‰« (Î±_ X.hom (G.map P.right) (G.map Q.right)).inv
         â‰« P.f â–· (G.map Q.right) â‰« (Î±_ (F.map P.left) Y.hom (G.map Q.right)).hom 
         â‰« (F.map P.left) â— Q.f â‰« (Î±_ (F.map P.left) (F.map Q.left) Z.hom).inv 
         â‰« (F.mapComp P.left Q.left) â–· Z.hom

/-- Underlying 1-CategoryStruct. -/
@[simps]
instance : CategoryStruct (Comma F G) where
  Hom X Y := Homâ‚ X Y
  id X := idâ‚ X
  comp P Q := compâ‚ P Q

/-- 2-cells. -/
@[ext]
structure Homâ‚‚ {X Y : Comma F G} (P Q : X âŸ¶ Y) where
  left : P.left âŸ¶ Q.left
  right : P.right âŸ¶ Q.right
  icc : P.f â‰« (F.mapâ‚‚ left â–· Y.hom) = (X.hom â— G.mapâ‚‚ right) â‰« Q.f := by cat_disch

/-- For a 1-cell `(p, q, Î¸)`, the identity 2-cell is `(ğŸ™p, ğŸ™q)`. -/
@[simps]
def idâ‚‚ {X Y : Comma F G} (P : X âŸ¶ Y) : Homâ‚‚ P P where
  left := ğŸ™ P.left
  right := ğŸ™ P.right

/- For two 2-cells `(Î±â‚€, Î²â‚€)`, (Î±â‚, Î²â‚)`, their composition is `(Î±â‚Î±â‚€, Î²â‚Î²â‚€)`. -/
@[simps]
def compâ‚‚ {X Y : Comma F G} {P Q R : X âŸ¶ Y} (Î· : Homâ‚‚ P Q) (Î¸ : Homâ‚‚ Q R) :
    Homâ‚‚ P R where
  left := Î·.left â‰« Î¸.left
  right := Î·.right â‰« Î¸.right
  icc := by simp only [PrelaxFunctor.mapâ‚‚_comp, comp_whiskerRight, whiskerLeft_comp, assoc]
            rw [â†assoc, Î·.icc, â†Î¸.icc, assoc]

/-- Hom category on 1-cells. -/
@[simp]
instance {X Y : Comma F G} : Category (X âŸ¶ Y) where
  Hom P Q := Homâ‚‚ P Q
  id P := idâ‚‚ P
  comp P Q := compâ‚‚ P Q

/-- Left whiskering, given directly by the left whiskering on the base categories. -/
@[simps]
def whiskerLeft {X Y Z : Comma F G} (P : X âŸ¶ Y) {Q R : Y âŸ¶ Z} (Î· : Q âŸ¶ R) : P â‰« Q âŸ¶ P â‰« R where
  left := P.left â— Î·.left
  right := P.right â— Î·.right
  icc := by simp only [comp_def, compâ‚_right, compâ‚_left, compâ‚_f, assoc]
            rw [â†comp_whiskerRight]
            simp only [LaxFunctor.mapComp_naturality_right, comp_whiskerRight, whisker_assoc,
              assoc, Iso.inv_hom_id_assoc]
            rw [â†assoc (F.map P.left â— Q.f), â†whiskerLeft_comp]
            simp only [Î·.icc, whiskerLeft_comp, assoc]
            rw [â†assoc (X.hom â— G.mapâ‚‚ (P.right â— Î·.right)), â†whiskerLeft_comp]
            simp only [OplaxFunctor.mapComp_naturality_right, whiskerLeft_comp, assoc]
            rw [â†assoc (X.hom â— G.map P.right â— G.mapâ‚‚ Î·.right), associator_inv_naturality_right,
              â†assoc ((Î±_ (F.map P.left) Y.hom (G.map Q.right)).hom), â†associator_naturality_right,
              assoc, assoc, â†assoc ((X.hom â‰« G.map P.right) â— G.mapâ‚‚ Î·.right), whisker_exchange]
            simp

/-- Right whiskering, given directly by the right whiskering on the base categories. -/
@[simps]
def whiskerRight {X Y Z : Comma F G} {P Q : X âŸ¶ Y} (Î· : P âŸ¶ Q) (R : Y âŸ¶ Z) : P â‰« R âŸ¶ Q â‰« R where
  left := Î·.left â–· R.left
  right := Î·.right â–· R.right
  icc := by simp only [comp_def, compâ‚_right, compâ‚_left, compâ‚_f, assoc]
            rw [â†comp_whiskerRight]
            simp only [LaxFunctor.mapComp_naturality_left, comp_whiskerRight]
            rw [â†assoc ((Î±_ (F.map P.left) (F.map R.left) Z.hom).inv),
              â†associator_inv_naturality_left, â†assoc (X.hom â— G.mapâ‚‚ (Î·.right â–· R.right)),
              â†whiskerLeft_comp, OplaxFunctor.mapComp_naturality_left, whiskerLeft_comp, assoc,
              assoc, whisker_assoc_symm, assoc, assoc,
              â†assoc ((Î±_ X.hom (G.map Q.right) (G.map R.right)).hom), Iso.hom_inv_id, id_comp,
              â†assoc ((X.hom â— G.mapâ‚‚ Î·.right) â–· G.map R.right ), â†comp_whiskerRight, â†Î·.icc,
              comp_whiskerRight, assoc, â†assoc (F.mapâ‚‚ Î·.left â–· Y.hom â–· G.map R.right),
              associator_naturality_left, â†assoc (F.map P.left â— R.f), whisker_exchange]
            simp

@[simps]
def associatorHom {X Y Z W : Comma F G} (P : X âŸ¶ Y) (Q : Y âŸ¶ Z) (R : Z âŸ¶ W) : 
    (P â‰« Q) â‰« R âŸ¶ P â‰« Q â‰« R where
  left := (Î±_ P.left Q.left R.left).hom
  right := (Î±_ P.right Q.right R.right).hom
  icc := by simp only [comp_def, compâ‚_right, compâ‚_left, compâ‚_f, comp_whiskerRight, 
              whisker_assoc, assoc, Iso.inv_hom_id_assoc, whiskerLeft_comp]
            rw [â†assoc (X.hom â— G.mapComp (P.right â‰« Q.right) R.right), â†whiskerLeft_comp, 
              OplaxFunctor.mapComp_assoc_left]
            simp only [whiskerLeft_comp, assoc, pentagon_inv_assoc]
            rw [â†assoc (X.hom â— G.map P.right â— G.mapComp Q.right R.right), 
              associator_inv_naturality_right, assoc, 
              â†assoc ((Î±_ (X.hom â‰« G.map P.right) (G.map Q.right) (G.map R.right)).inv), 
              â†associator_inv_naturality_left, assoc, 
              â†assoc ((X.hom â‰« G.map P.right) â— G.mapComp Q.right R.right), whisker_exchange]
            simp only [comp_whiskerLeft, pentagon_inv_hom_hom_hom_inv_assoc, assoc, 
              Iso.inv_hom_id_assoc]
            rw [â†assoc (F.mapComp P.left Q.left â–· Z.hom â–· G.map R.right), 
              associator_naturality_left, assoc, 
              â†assoc ((Î±_ (F.map P.left) (F.map Q.left â‰« Z.hom) (G.map R.right)).inv), 
              â†assoc ((Î±_ (F.map P.left) (F.map Q.left â‰« Z.hom) (G.map R.right)).inv â‰« 
               (Î±_ (F.map P.left) (F.map Q.left) Z.hom).inv â–· G.map R.right), 
               assoc ((Î±_ (F.map P.left) (F.map Q.left â‰« Z.hom) (G.map R.right)).inv), 
               pentagon_inv_inv_hom_hom_inv, assoc, 
               â†assoc (F.mapComp P.left Q.left â–· (Z.hom â‰« G.map R.right)), â†whisker_exchange]
            simp only [comp_whiskerLeft, whiskerRight_comp, assoc, Iso.hom_inv_id_assoc, 
              Iso.inv_hom_id_assoc]
            rw [â†comp_whiskerRight, â†comp_whiskerRight]
            simp

@[simps]
def associatorInv {X Y Z W : Comma F G} (P : X âŸ¶ Y) (Q : Y âŸ¶ Z) (R : Z âŸ¶ W) :
    P â‰« Q â‰« R âŸ¶ (P â‰« Q) â‰« R where
  left := (Î±_ P.left Q.left R.left).inv
  right := (Î±_ P.right Q.right R.right).inv
  icc := by simp only [comp_def, compâ‚_right, compâ‚_left, compâ‚_f, whiskerLeft_comp, assoc, 
              comp_whiskerRight, whisker_assoc, Iso.inv_hom_id_assoc]
            rw [â†assoc (X.hom â— G.mapComp (P.right â‰« Q.right) R.right), â†whiskerLeft_comp, 
              OplaxFunctor.mapComp_assoc_left, 
              â†assoc (X.hom â— G.mapâ‚‚ (Î±_ P.right Q.right R.right).inv), â†whiskerLeft_comp, 
              â†assoc (G.mapâ‚‚ (Î±_ P.right Q.right R.right).inv), â†PrelaxFunctor.mapâ‚‚_comp]
            simp only [Iso.inv_hom_id, PrelaxFunctor.mapâ‚‚_id, id_comp, whiskerLeft_comp, 
              assoc, pentagon_inv_assoc]
            rw [â†assoc (X.hom â— G.map P.right â— G.mapComp Q.right R.right), 
              associator_inv_naturality_right, assoc, 
              â†assoc (Î±_ (X.hom â‰« G.map P.right) (G.map Q.right) (G.map R.right)).inv, 
              â†associator_inv_naturality_left, 
              â†assoc ((Î±_ (F.map P.left) Y.hom (G.map (Q.right â‰« R.right))).hom),
              â†associator_naturality_right, assoc, â†assoc (P.f â–· G.map (Q.right â‰« R.right)), 
              â†whisker_exchange]
            simp only [comp_whiskerLeft, whiskerRight_comp, assoc, 
              pentagon_hom_hom_inv_hom_hom_assoc, Iso.inv_hom_id_assoc, Iso.hom_inv_id, comp_id]
            rw [â†assoc (F.mapComp P.left Q.left â–· Z.hom â–· G.map R.right), 
              associator_naturality_left, assoc, 
              â†assoc ((Î±_ (F.map P.left) (F.map Q.left â‰« Z.hom) (G.map R.right)).inv), 
              â†assoc (((Î±_ (F.map P.left) (F.map Q.left â‰« Z.hom) (G.map R.right)).inv â‰« 
              (Î±_ (F.map P.left) (F.map Q.left) Z.hom).inv â–· G.map R.right)), 
              assoc ((Î±_ (F.map P.left) (F.map Q.left â‰« Z.hom) (G.map R.right)).inv), 
              pentagon_inv_inv_hom_hom_inv, assoc, 
              â†assoc (F.mapComp P.left Q.left â–· (Z.hom â‰« G.map R.right)), â†whisker_exchange]
            simp only [comp_whiskerLeft, whiskerRight_comp, assoc, Iso.hom_inv_id_assoc, 
              Iso.inv_hom_id_assoc]
            rw [â†assoc ((Î±_ (F.map P.left â‰« F.map Q.left) (F.map R.left) W.hom).inv), 
              â†associator_inv_naturality_left, assoc, 
              â†assoc (F.map P.left â— F.mapComp Q.left R.left â–· W.hom), 
              associator_inv_naturality_middle, assoc, 
              â†assoc ((F.map P.left â— F.mapComp Q.left R.left) â–· W.hom), â†comp_whiskerRight, 
              LaxFunctor.mapComp_assoc_right, comp_whiskerRight, assoc, â†comp_whiskerRight, 
              assoc, assoc, â†PrelaxFunctor.mapâ‚‚_comp]
            simp
            
/-- Associator, given directly by the associator on the base categories. -/
@[simps]
def associator {X Y Z W : Comma F G} (P : X âŸ¶ Y) (Q : Y âŸ¶ Z) (R : Z âŸ¶ W) : 
    (P â‰« Q) â‰« R â‰… P â‰« Q â‰« R where
  hom := associatorHom P Q R
  inv := associatorInv P Q R

@[simps]
def leftUnitorHom {X Y : Comma F G} (P : X âŸ¶ Y) : ğŸ™ X â‰« P âŸ¶ P where
  left := (Î»_ P.left).hom
  right := (Î»_ P.right).hom
  icc := by simp only [id_def, comp_def, compâ‚_right, idâ‚_right, compâ‚_left, idâ‚_left, compâ‚_f, 
              idâ‚_f, comp_whiskerRight, whisker_assoc, leftUnitor_inv_whiskerRight, assoc, 
              triangle_assoc_comp_right_assoc, Iso.inv_hom_id_assoc, OplaxFunctor.mapâ‚‚_leftUnitor, 
              whiskerLeft_comp]
            rw [â†assoc (F.mapId X.left â–· X.hom â–· G.map P.right), associator_naturality_left, assoc, 
              â†assoc ((Î±_ (ğŸ™ (F.obj X.left)) X.hom (G.map P.right)).inv), Iso.inv_hom_id, id_comp, 
              â†assoc (F.mapId X.left â–· (X.hom â‰« G.map P.right)), â†whisker_exchange]
            simp only [id_whiskerLeft, whiskerRight_comp, assoc, Iso.hom_inv_id_assoc, 
              Iso.inv_hom_id_assoc]
            rw [â†comp_whiskerRight, â†comp_whiskerRight, â†LaxFunctor.mapâ‚‚_leftUnitor_hom]
            simp

@[simps]
def leftUnitorInv {X Y : Comma F G} (P : X âŸ¶ Y) : P âŸ¶ ğŸ™ X â‰« P where
  left := (Î»_ P.left).inv
  right := (Î»_ P.right).inv
  icc := by simp only [id_def, comp_def, compâ‚_left, idâ‚_left, LaxFunctor.mapâ‚‚_leftUnitor, 
              comp_whiskerRight, leftUnitor_inv_whiskerRight, assoc, compâ‚_right, idâ‚_right, 
              compâ‚_f, idâ‚_f, whisker_assoc, triangle_assoc_comp_right_assoc, Iso.inv_hom_id_assoc]
            rw [â†assoc (F.mapId X.left â–· X.hom â–· G.map P.right), associator_naturality_left, assoc, 
              â†assoc ((Î±_ (ğŸ™ (F.obj X.left)) X.hom (G.map P.right)).inv), Iso.inv_hom_id, id_comp, 
              â†assoc (F.mapId X.left â–· (X.hom â‰« G.map P.right)), â†whisker_exchange]
            simp only [id_whiskerLeft, whiskerRight_comp, assoc, Iso.hom_inv_id_assoc, 
              Iso.inv_hom_id_assoc]
            rw [â†assoc (X.hom â— G.mapComp (ğŸ™ X.right) P.right), â†whiskerLeft_comp, 
              â†assoc (X.hom â— (G.mapComp (ğŸ™ X.right) P.right â‰« G.mapId X.right â–· G.map P.right)), 
              â†whiskerLeft_comp, assoc, â†OplaxFunctor.mapâ‚‚_leftUnitor, 
              â†assoc (X.hom â— G.mapâ‚‚ (Î»_ P.right).inv), â†whiskerLeft_comp, â†PrelaxFunctor.mapâ‚‚_comp]
            simp
            
/-- Left unitor, given directly by the left unitor on the base categories. -/
@[simps]
def leftUnitor {X Y : Comma F G} (P : X âŸ¶ Y) : ğŸ™ X â‰« P â‰… P where
  hom := leftUnitorHom P
  inv := leftUnitorInv P

@[simps]
def rightUnitorHom {X Y : Comma F G} (P : X âŸ¶ Y) : P â‰« ğŸ™ Y âŸ¶ P where
  left := (Ï_ P.left).hom
  right := (Ï_ P.right).hom
  icc := by simp only [id_def, comp_def, compâ‚_right, idâ‚_right, compâ‚_left, idâ‚_left, compâ‚_f, 
              idâ‚_f, whiskerLeft_comp, whiskerLeft_rightUnitor, assoc, 
              OplaxFunctor.mapâ‚‚_rightUnitor]
            rw [â†assoc (F.map P.left â— Y.hom â— G.mapId Y.right), associator_inv_naturality_right, 
              assoc, â†assoc ((Î±_ (F.map P.left) Y.hom (G.map (ğŸ™ Y.right))).hom), Iso.hom_inv_id, 
              id_comp, â†assoc (P.f â–· G.map (ğŸ™ Y.right)), â†whisker_exchange]
            simp only [comp_whiskerLeft, whiskerRight_id, assoc, Iso.inv_hom_id_assoc]
            rw [â†assoc (F.map P.left â— F.mapId Y.left â–· Y.hom), associator_inv_naturality_middle, 
            assoc, â†comp_whiskerRight, â†comp_whiskerRight, â†LaxFunctor.mapâ‚‚_rightUnitor_hom]
            simp

@[simps]
def rightUnitorInv {X Y : Comma F G} (P : X âŸ¶ Y) : P âŸ¶ P â‰« ğŸ™ Y where
  left := (Ï_ P.left).inv
  right := (Ï_ P.right).inv
  icc := by simp only [id_def, comp_def, compâ‚_left, idâ‚_left, LaxFunctor.mapâ‚‚_rightUnitor, 
              comp_whiskerRight, whisker_assoc, assoc, triangle_assoc_comp_right_inv_assoc, 
              compâ‚_right, idâ‚_right, compâ‚_f, idâ‚_f, whiskerLeft_comp, whiskerLeft_rightUnitor]
            rw [â†assoc (F.map P.left â— Y.hom â— G.mapId Y.right), associator_inv_naturality_right, 
              assoc, â†assoc ((Î±_ (F.map P.left) Y.hom (G.map (ğŸ™ Y.right))).hom), Iso.hom_inv_id, 
              id_comp, â†assoc (P.f â–· G.map (ğŸ™ Y.right)), â†whisker_exchange]
            simp only [comp_whiskerLeft, whiskerRight_id, assoc, Iso.inv_hom_id_assoc]
            rw [rightUnitor_comp, assoc, 
              â†assoc ((Î±_ X.hom (G.map P.right) (ğŸ™ (G.obj Y.right))).inv), Iso.inv_hom_id, id_comp, 
              â†assoc (X.hom â— G.mapComp P.right (ğŸ™ Y.right)), â†whiskerLeft_comp, 
              â†assoc (X.hom â— (G.mapComp P.right (ğŸ™ Y.right) â‰« G.map P.right â— G.mapId Y.right)), 
              â†whiskerLeft_comp, assoc, â†OplaxFunctor.mapâ‚‚_rightUnitor, 
              â†assoc (X.hom â— G.mapâ‚‚ (Ï_ P.right).inv), â†whiskerLeft_comp, â†PrelaxFunctor.mapâ‚‚_comp]
            simp 

/-- Right unitor, given directly by the right unitor on the base categories. -/
@[simps]
def rightUnitor {X Y : Comma F G} (P : X âŸ¶ Y) : P â‰« ğŸ™ Y â‰… P where
  hom := rightUnitorHom P
  inv := rightUnitorInv P

/-- Comma bicategory. -/
@[simp]
instance : Bicategory (Comma F G) where
  whiskerLeft P _ _ Î· := whiskerLeft P Î·
  whiskerRight Î· R := whiskerRight Î· R
  associator P Q R := associator P Q R
  leftUnitor P := leftUnitor P
  rightUnitor P := rightUnitor P
  whisker_exchange Î· Î¸ := by simp only [instCategoryHom, comp_def]
                             ext
                             Â· simp only [compâ‚_left, compâ‚‚_left, whiskerLeft_left, 
                               whiskerRight_left]
                               rw [whisker_exchange]
                             simp only [compâ‚_right, compâ‚‚_right, whiskerLeft_right, 
                               whiskerRight_right]
                             rw [whisker_exchange]

@[simp]
theorem eqToHom_left {X Y : Comma F G} {P Q : X âŸ¶ Y} (e : P = Q) : 
    (eqToHom e).left = eqToHom (congrArg Homâ‚.left e) := by
  cases e
  simp

@[simp]
theorem eqToHom_right {X Y : Comma F G} {P Q : X âŸ¶ Y} (e : P = Q) : 
    (eqToHom e).right = eqToHom (congrArg Homâ‚.right e) := by
  cases e
  simp

namespace mapLeft

variable {H : A â¥¤á´¸ T} (Î· : Lax.LaxTrans H F)

/-- Action of the change-of-left-leg functor on objects. -/
@[simps]
def obj (X : Comma F G) : Comma H G where 
  left := X.left 
  right := X.right 
  hom := (Î·.app X.left â‰« X.hom)

/-- Action of the change-of-left-leg functor on 1-cells. -/
@[simps]
def map {X Y : Comma F G} (P : X âŸ¶ Y) : (obj Î· X âŸ¶ obj Î· Y) where 
  left := P.left 
  right := P.right 
  f := ((Î±_ (Î·.app X.left) X.hom (G.map P.right)).hom â‰« (Î·.app X.left) â— P.f â‰« 
    (Î±_ (Î·.app X.left) (F.map P.left) Y.hom).inv â‰« (Î·.naturality P.left) â–· Y.hom â‰« 
    (Î±_ (H.map P.left) (Î·.app Y.left) Y.hom).hom)

/-- Action of the change-of-left-leg functor on 2-cells. -/
@[simps]
def mapâ‚‚ {X Y : Comma F G} {P Q : X âŸ¶ Y} (Î¸ : P âŸ¶ Q) : (map Î· P âŸ¶ map Î· Q) where 
  left := Î¸.left 
  right := Î¸.right 
  icc := by simp only [obj_left, obj_right, obj_hom, map_right, map_left, map_f, whiskerRight_comp, 
              assoc, Iso.hom_inv_id_assoc, comp_whiskerLeft, Iso.inv_hom_id_assoc, 
              Iso.cancel_iso_hom_left]
            rw [â†assoc (Î·.naturality P.left â–· Y.hom), â†comp_whiskerRight, 
              Lax.LaxTrans.naturality_naturality]
            simp only [comp_whiskerRight, whisker_assoc, assoc, Iso.inv_hom_id_assoc]
            rw [â†assoc (Î·.app X.left â— P.f), â†whiskerLeft_comp, Î¸.icc] 
            simp

@[simp]
theorem map_id (X : Comma F G) : map Î· (ğŸ™ X) = ğŸ™ (obj Î· X) := by
  simp only [id_def]
  apply Homâ‚.ext
  all_goals simp only [obj_left, obj_right, obj_hom, map_right, Comma.idâ‚_right, map_left, 
    Comma.idâ‚_left, map_f, Comma.idâ‚_f, whiskerLeft_comp, whiskerLeft_rightUnitor, assoc, 
    comp_whiskerLeft, whiskerRight_comp, heq_eq_eq, Iso.cancel_iso_hom_left]
  rw [â†assoc (Î·.app X.left â— F.mapId X.left â–· X.hom), associator_inv_naturality_middle, assoc, 
    â†assoc ((Î·.app X.left â— F.mapId X.left) â–· X.hom), â†comp_whiskerRight, 
    Lax.LaxTrans.naturality_id]
  simp

@[simp]
theorem map_comp {X Y Z : Comma F G} (P : X âŸ¶ Y) (Q : Y âŸ¶ Z) : 
    map Î· (P â‰« Q) = map Î· P â‰« map Î· Q := by
  simp only [Comma.comp_def]
  apply Homâ‚.ext
  all_goals simp only [obj_left, obj_right, obj_hom, map_right, Comma.compâ‚_right, map_left, 
    Comma.compâ‚_left, map_f, Comma.compâ‚_f, whiskerLeft_comp, assoc, comp_whiskerLeft, 
    comp_whiskerRight, whisker_assoc, whiskerRight_comp,
    pentagon_hom_inv_inv_inv_inv_assoc, pentagon_assoc, pentagon_inv_hom_hom_hom_inv_assoc, 
    Iso.inv_hom_id_assoc, heq_eq_eq, Iso.cancel_iso_hom_left]
  rw [â†assoc (Î·.app X.left â— F.mapComp P.left Q.left â–· Z.hom), associator_inv_naturality_middle, 
    assoc, â†assoc ((Î·.app X.left â— F.mapComp P.left Q.left) â–· Z.hom), â†comp_whiskerRight, 
    Lax.LaxTrans.naturality_comp]
  simp only [comp_whiskerRight, whisker_assoc, assoc, pentagon_inv_assoc]
  rw [â†assoc (Î·.app X.left â— F.map P.left â— Q.f), associator_inv_naturality_right, assoc, 
    â†assoc (Î·.naturality P.left â–· Y.hom â–· G.map Q.right), 
    associator_naturality_left (Î·.naturality P.left), assoc, 
    â†assoc ((Î±_ (Î·.app X.left) (F.map P.left â‰« Y.hom) (G.map Q.right)).inv), 
    â†assoc ((Î±_ (Î·.app X.left) (F.map P.left â‰« Y.hom) (G.map Q.right)).inv â‰« 
    (Î±_ (Î·.app X.left) (F.map P.left) Y.hom).inv â–· G.map Q.right), 
    assoc ((Î±_ (Î·.app X.left) (F.map P.left â‰« Y.hom) (G.map Q.right)).inv), 
    pentagon_inv_inv_hom_hom_inv, assoc, 
    â†assoc ((Î±_ (Î·.app X.left â‰« F.map P.left) (F.map Q.left) Z.hom).inv), 
    â†associator_inv_naturality_left, assoc, â†assoc ((Î·.app X.left â‰« F.map P.left) â— Q.f), 
    whisker_exchange]
  simp

@[simp]
theorem mapâ‚‚_whisker_left {X Y Z : Comma F G} (P : X âŸ¶ Y) {Q R : Y âŸ¶ Z} (Î¸ : Q âŸ¶ R) : 
    mapâ‚‚ Î· (P â— Î¸) = eqToHom (map_comp Î· P Q) â‰« map Î· P â— mapâ‚‚ Î· Î¸ 
    â‰« eqToHom (map_comp Î· P R).symm := by
  simp only [Comma.instCategoryHom, Comma.inst, Comma.comp_def]
  ext
  Â· simp only [obj_left, map_left, Comma.compâ‚_left, mapâ‚‚_left, Comma.whiskerLeft_left, 
      Comma.compâ‚‚_left]
    rw [Comma.eqToHom_left, Comma.eqToHom_left]
    simp
  simp only [obj_right, map_right, Comma.compâ‚_right, mapâ‚‚_right, Comma.whiskerLeft_right, 
    Comma.compâ‚‚_right]
  rw [Comma.eqToHom_right, Comma.eqToHom_right]
  simp
  
@[simp]
theorem mapâ‚‚_whisker_right {X Y Z : Comma F G} {P Q : X âŸ¶ Y} (Î¸ : P âŸ¶ Q) (R : Y âŸ¶ Z) : 
    mapâ‚‚ Î· (Î¸ â–· R) = eqToHom (map_comp Î· P R) â‰« mapâ‚‚ Î· Î¸ â–· map Î· R 
    â‰« eqToHom (map_comp Î· Q R).symm := by
  simp only [Comma.instCategoryHom, Comma.inst, Comma.comp_def]
  ext
  Â· simp only [obj_left, map_left, Comma.compâ‚_left, mapâ‚‚_left, Comma.whiskerRight_left, 
      Comma.compâ‚‚_left]
    rw [Comma.eqToHom_left, Comma.eqToHom_left]
    simp
  simp only [obj_right, map_right, Comma.compâ‚_right, mapâ‚‚_right, Comma.whiskerRight_right, 
    Comma.compâ‚‚_right]
  rw [Comma.eqToHom_right, Comma.eqToHom_right]
  simp

@[simp]
theorem mapâ‚‚_left_unitor {X Y : Comma F G} (P : X âŸ¶ Y) : 
    mapâ‚‚ Î· (Î»_ P).hom = eqToHom (by rw [map_comp Î· (ğŸ™ X) P, map_id Î· X]) â‰« (Î»_ (map Î· P)).hom := by
  simp only [Comma.instCategoryHom, Comma.inst, Comma.id_def, Comma.comp_def, 
    Comma.leftUnitor_hom]
  ext
  Â· simp only [obj_left, map_left, Comma.compâ‚_left, Comma.idâ‚_left, mapâ‚‚_left, 
      Comma.leftUnitorHom_left, Comma.compâ‚‚_left]
    rw [Comma.eqToHom_left]
    simp
  simp only [obj_right, map_right, Comma.compâ‚_right, Comma.idâ‚_right, mapâ‚‚_right, 
    Comma.leftUnitorHom_right, Comma.compâ‚‚_right]
  rw [Comma.eqToHom_right]
  simp

@[simp]
theorem mapâ‚‚_right_unitor {X Y : Comma F G} (P : X âŸ¶ Y) : 
    mapâ‚‚ Î· (Ï_ P).hom = eqToHom (by rw [map_comp Î· P (ğŸ™ Y), map_id Î· Y]) â‰« (Ï_ (map Î· P)).hom := by
  simp only [Comma.instCategoryHom, Comma.inst, Comma.id_def, Comma.comp_def, 
    Comma.rightUnitor_hom]
  ext
  Â· simp only [obj_left, map_left, Comma.compâ‚_left, Comma.idâ‚_left, mapâ‚‚_left, 
      Comma.rightUnitorHom_left, Comma.compâ‚‚_left]
    rw [Comma.eqToHom_left]
    simp
  simp only [obj_right, map_right, Comma.compâ‚_right, Comma.idâ‚_right, mapâ‚‚_right, 
    Comma.rightUnitorHom_right, Comma.compâ‚‚_right]
  rw [Comma.eqToHom_right]
  simp

@[simp]
theorem mapâ‚‚_associator {X Y Z W : Comma F G} (P : X âŸ¶ Y) (Q : Y âŸ¶ Z) (R : Z âŸ¶ W) : 
    mapâ‚‚ Î· (Î±_ P Q R).hom = eqToHom (by simp only [map_comp]) â‰« 
    (Î±_ (map Î· P) (map Î· Q) (map Î· R)).hom â‰« eqToHom (by simp only [map_comp]) := by
  simp only [Comma.instCategoryHom, Comma.inst, Comma.comp_def, Comma.associator_hom]
  ext
  Â· simp only [obj_left, map_left, Comma.compâ‚_left, mapâ‚‚_left, Comma.associatorHom_left, 
    Comma.compâ‚‚_left]
    rw [Comma.eqToHom_left, Comma.eqToHom_left]
    simp
  simp only [obj_right, map_right, Comma.compâ‚_right, mapâ‚‚_right, Comma.associatorHom_right, 
    Comma.compâ‚‚_right]
  rw [Comma.eqToHom_right, Comma.eqToHom_right]
  simp

@[simps]
def core : StrictPseudofunctorCore (Comma F G) (Comma H G) where
  obj := obj Î·
  map := map Î·
  mapâ‚‚ := mapâ‚‚ Î·
  map_id := map_id Î·
  map_comp := map_comp Î·
  mapâ‚‚_whisker_left := mapâ‚‚_whisker_left Î·
  mapâ‚‚_whisker_right := mapâ‚‚_whisker_right Î·
  mapâ‚‚_left_unitor := mapâ‚‚_left_unitor Î·
  mapâ‚‚_right_unitor := mapâ‚‚_right_unitor Î·
  mapâ‚‚_associator := mapâ‚‚_associator Î·

/-- The change of left leg strict pseudofunctor. -/
@[simps!]
def functor : StrictPseudofunctor (Comma F G) (Comma H G) := StrictPseudofunctor.mk' (core Î·)

end mapLeft 

namespace mapRight

variable {H : B â¥¤áµ’áµ–á´¸ T} (Î· : Oplax.LaxTrans G H)

end mapRight
