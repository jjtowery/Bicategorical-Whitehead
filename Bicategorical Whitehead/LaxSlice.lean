/-
  Copyright (c) 2025 Judah Towery. All rights reserved.
  Released under Apache 2.0 license as described in the file LICENSE.
  Authors: Judah Towery
-/

import Mathlib.CategoryTheory.Bicategory.Functor.Lax

/-!

# The lax slice bicategory F â†“ X of a lax functor F : B â¥¤á´¸ C over an object X : C

* objects are pairs (A : B, f_A : FA âŸ¶ X) 
* 1-cells are pairs (p : Aâ‚€ âŸ¶ Aâ‚, Î¸_p : fâ‚€ âŸ¶ fâ‚(Fp) in C
* 2-cells are 2-cells Î± : pâ‚€ âŸ¶ pâ‚ in B with FÎ± subject to the ice cream cone condition.

Provides a change-of-slice strict pseudofunctor for a 1-cell u : X âŸ¶ Y,
F â†“ u : (F â†“ X) â¥¤áµ– (F â†“ Y)

## References
* [Niles Johnson, Donald Yau, *2-Dimensional Categories*](https://arxiv.org/abs/2002.06055),
section 7.1
-/

namespace CategoryTheory

open Category Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚

namespace LaxSlice

variable {B C : Type*} [Bicategory.{wâ‚, vâ‚} B] [Bicategory.{wâ‚‚, vâ‚‚} C]

variable (F : B â¥¤á´¸ C) (X : C)

/-- Objects of the lax slice bicategory `F â†“ X`. -/
@[ext]
structure Obj where
  A : B
  f : F.obj A âŸ¶ X

scoped notation F " â†“ " X => Obj F X

/-- 1-cells in `F â†“ X`
A 1-cell `(Aâ‚€, fâ‚€) âŸ¶ (Aâ‚, fâ‚)` is a pair `(p, Î¸_p)` with 
`p : Aâ‚€ âŸ¶ Aâ‚` in `B`, and `Î¸_p : fâ‚€ âŸ¶ fâ‚(Fp)` in `C`.
This is depicted as a triangle
```
FAâ‚€-----Fp----->FAâ‚
|               |
|    â‡’â‡’Î¸_pâ‡’â‡’    |
|               |
|--fâ‚€-->X<--fâ‚--| 
``` -/
@[ext]
structure Homâ‚ (Aâ‚€ Aâ‚ : F â†“ X) where
  p : Aâ‚€.A âŸ¶ Aâ‚.A
  Î¸ : Aâ‚€.f âŸ¶ F.map p â‰« Aâ‚.f

/-- Identity 1-cell 
For an object `(A, f)`, the identity 1-cell is `(1_A, r')`, with `r'` from this pasting diagram:
```
|-------F1_A------|
|        â‡‘        |
|      F^0_A      |
|        â‡‘        â†“
FA------1_FA----->FA
|                 |
|     â‡’â‡’r^-1â‡’â‡’    |
|                 |
|--f_A-->X<--f_A--| 
``` -/
@[simps]
def idâ‚ (A : F â†“ X) : Homâ‚ F X A A where
  p := ðŸ™ A.A
  Î¸ := (Î»_ A.f).inv â‰« (F.mapId A.A â–· A.f)

/-- Composition of 1-cells.
For 1-cells `(pâ‚€, Î¸â‚€) : (Aâ‚€, fâ‚€) âŸ¶ (Aâ‚, fâ‚), (pâ‚, Î¸â‚) : (Aâ‚, fâ‚) âŸ¶ (Aâ‚‚, fâ‚‚)`, their composite is
`(pâ‚pâ‚€, Î¸')`, where `Î¸'` is formed from the composite of the pasting diagram:
```
|-------F(pâ‚pâ‚€)-------|          
|          â‡‘          |
|       F^2_{pâ‚,pâ‚€}   |
|          â‡‘          â†“
FAâ‚€--Fpâ‚€-->FAâ‚--Fpâ‚-->FAâ‚‚
|          |          |
|  â‡’â‡’Î¸â‚€â‡’â‡’  fâ‚ â‡’â‡’Î¸â‚â‡’â‡’  |
|          â†“          |
|----fâ‚€--->X<---fâ‚‚----| 
``` -/
@[simps]
def compâ‚ {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} (pâ‚€ : Homâ‚ F X Aâ‚€ Aâ‚) (pâ‚ : Homâ‚ F X Aâ‚ Aâ‚‚) : Homâ‚ F X Aâ‚€ Aâ‚‚ where
  p := pâ‚€.p â‰« pâ‚.p
  Î¸ := pâ‚€.Î¸ â‰« (F.map pâ‚€.p â— pâ‚.Î¸) â‰« (Î±_ (F.map pâ‚€.p) (F.map pâ‚.p) Aâ‚‚.f).inv 
       â‰« (F.mapComp pâ‚€.p pâ‚.p â–· Aâ‚‚.f)

/-- Underlying CategoryStruct on objects. -/
@[simps]
instance : CategoryStruct (F â†“ X) where
  Hom Aâ‚€ Aâ‚ := Homâ‚ F X Aâ‚€ Aâ‚
  id A := idâ‚ F X A
  comp Aâ‚€ Aâ‚ := compâ‚ F X Aâ‚€ Aâ‚

/-- 2-cells in `F â†“ X`
A 2-cell `(pâ‚€, Î¸â‚€) âŸ¶ (pâ‚, Î¸â‚)` is a 2-cell `Î± : pâ‚€ âŸ¶ pâ‚` in `B` such that
`FÎ±` satisfies the ice cream cone condition:
```        
|-------Fpâ‚-----|     FAâ‚€-----Fpâ‚---->FAâ‚
|        â‡‘      |     |               |
|       FÎ±      |     |               |
|        â‡‘      â†“     |               |
FAâ‚€-----Fpâ‚€---->FAâ‚ = |     â‡’â‡’Î¸â‚â‡’â‡’    |                
|               |     |               |
|     â‡’â‡’Î¸â‚€â‡’â‡’    |     |               |
|               |     |               |
|--fâ‚€-->X<--fâ‚--|     |--fâ‚€-->X<--fâ‚--| 
``` -/
@[ext]
structure Homâ‚‚ {Aâ‚€ Aâ‚ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚€ âŸ¶ Aâ‚) where
  Î± : pâ‚€.p âŸ¶ pâ‚.p
  icc : pâ‚€.Î¸ â‰« (F.mapâ‚‚ Î± â–· Aâ‚.f) = pâ‚.Î¸

/-- Identity 2-cell.
For a 1-cell `(p, Î¸)`, the identity 2-cell is `1_p` -/
@[simps]
def idâ‚‚ {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : Homâ‚‚ F X p p where
  Î± := ðŸ™ p.p
  icc := by simp

/-- Vertical composition of 2-cells.
For 1-cells `(p, Î¸), (p', Î¸'), (p'', Î¸'') : (Aâ‚€, Fâ‚€) âŸ¶ (Aâ‚, Fâ‚)`
and 2-cells `Î± : (p, Î¸) âŸ¶ (p', Î¸'), Î±' : (p', Î¸') âŸ¶ (p'', Î¸'')`,
their vertical composite is the composite `Î±'Î± : (p, Î¸) âŸ¶ (p'', Î¸'')`. -/
@[simps]
def compâ‚‚ {Aâ‚€ Aâ‚ : F â†“ X} {p p' p'' : Aâ‚€ âŸ¶ Aâ‚} (Î± : Homâ‚‚ F X p p') (Î±' : Homâ‚‚ F X p' p'') : 
    Homâ‚‚ F X p p'' where
  Î± := Î±.Î± â‰« Î±'.Î±
  icc := by simp [â†Î±.icc, â†Î±'.icc]

/-- Category structure on 1-cells with vertical composition. -/
@[simps!]
instance (Aâ‚€ Aâ‚ : F â†“ X) : Category (Aâ‚€ âŸ¶ Aâ‚) where
  Hom pâ‚€ pâ‚ := Homâ‚‚ F X pâ‚€ pâ‚
  id p := idâ‚‚ F X p
  comp Î±â‚€ Î±â‚ := compâ‚‚ F X Î±â‚€ Î±â‚

/-- Whisker a 2-cell on the left by a 1-cell.
Comes precisely from the whiskering on `B`. -/
@[simps]
def whiskerLeft {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) {pâ‚ pâ‚‚ : Aâ‚ âŸ¶ Aâ‚‚} (Î± : pâ‚ âŸ¶ pâ‚‚) : 
    (pâ‚€ â‰« pâ‚) âŸ¶ (pâ‚€ â‰« pâ‚‚) where
  Î± := pâ‚€.p â— Î±.Î±
  icc := by simp [â†Î±.icc, â†comp_whiskerRight]
            simp

@[simp]
theorem whiskerLeft_id {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) : 
    whiskerLeft F X pâ‚€ (ðŸ™ pâ‚) = ðŸ™ (pâ‚€ â‰« pâ‚) := by
  refine Homâ‚‚.ext ?_
  change pâ‚€.p â— ðŸ™ pâ‚.p = ðŸ™ _
  simp

@[simp]
theorem whiskerLeft_comp {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) {pâ‚ pâ‚‚ pâ‚ƒ : Aâ‚ âŸ¶ Aâ‚‚} (Î±â‚€ : pâ‚ âŸ¶ pâ‚‚) 
    (Î±â‚ : pâ‚‚ âŸ¶ pâ‚ƒ) : 
    whiskerLeft F X pâ‚€ (Î±â‚€ â‰« Î±â‚) = whiskerLeft F X pâ‚€ Î±â‚€ â‰« whiskerLeft F X pâ‚€ Î±â‚ := by
  refine Homâ‚‚.ext ?_
  change pâ‚€.p â— (Î±â‚€.Î± â‰« Î±â‚.Î±) = _ â‰« _ 
  simp

/-- Whisker a 2-cell on the right by a 1-cell.
Comes precisely from the whiskering on `B`. -/
@[simps]
def whiskerRight {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} {pâ‚€ pâ‚ : Aâ‚€ âŸ¶ Aâ‚} (Î± : pâ‚€ âŸ¶ pâ‚) (pâ‚‚ : Aâ‚ âŸ¶ Aâ‚‚) : 
    (pâ‚€ â‰« pâ‚‚) âŸ¶ (pâ‚ â‰« pâ‚‚) where
  Î± := Î±.Î± â–· pâ‚‚.p
  icc := by simp [â†Î±.icc, â†assoc (F.mapâ‚‚ Î±.Î± â–· Aâ‚.f), â†whisker_exchange, â†comp_whiskerRight]

@[simp]
theorem id_whiskerRight {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) : 
    whiskerRight F X (ðŸ™ pâ‚€) pâ‚ = ðŸ™ (pâ‚€ â‰« pâ‚) := by
  refine Homâ‚‚.ext ?_
  change (ðŸ™ pâ‚€.p) â–· pâ‚.p = ðŸ™ _
  simp


@[simp]
theorem comp_whiskerRight {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} {pâ‚€ pâ‚ pâ‚‚ : Aâ‚€ âŸ¶ Aâ‚} (Î±â‚€ : pâ‚€ âŸ¶ pâ‚) (Î±â‚ : pâ‚ âŸ¶ pâ‚‚) 
    (pâ‚ƒ : Aâ‚ âŸ¶ Aâ‚‚) : whiskerRight F X (Î±â‚€ â‰« Î±â‚) pâ‚ƒ = 
    whiskerRight F X Î±â‚€ pâ‚ƒ â‰« whiskerRight F X Î±â‚ pâ‚ƒ := by
  refine Homâ‚‚.ext ?_
  change (Î±â‚€.Î± â‰« Î±â‚.Î±) â–· pâ‚ƒ.p = _ â‰« _
  simp

/- Associator forward direction. -/
@[simps]
def associatorHom {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) (pâ‚‚ : Aâ‚‚ âŸ¶ Aâ‚ƒ) : 
    (pâ‚€ â‰« pâ‚) â‰« pâ‚‚ âŸ¶ pâ‚€ â‰« pâ‚ â‰« pâ‚‚ where
  Î± := by simpa using (Î±_ pâ‚€.p pâ‚.p pâ‚‚.p).hom
  icc := by simp [â†assoc (F.mapComp pâ‚€.p pâ‚.p â–· Aâ‚‚.f), â†whisker_exchange, 
                  â†Bicategory.comp_whiskerRight]
            rw [whisker_assoc_symm]
            simp

/- Associator reverse direction -/
@[simps]
def associatorInv {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) (pâ‚‚ : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
    pâ‚€ â‰« pâ‚ â‰« pâ‚‚ âŸ¶ (pâ‚€ â‰« pâ‚) â‰« pâ‚‚ where
  Î± := by simpa using (Î±_ pâ‚€.p pâ‚.p pâ‚‚.p).inv
  icc := by simp [â†assoc (F.mapComp pâ‚€.p pâ‚.p â–· Aâ‚‚.f), â†whisker_exchange]
            rw [â†assoc ((Î±_ (F.map pâ‚€.p) (F.map pâ‚.p) (F.map pâ‚‚.p â‰« Aâ‚ƒ.f)).inv), â†pentagon_inv, 
                assoc, whisker_assoc_symm, assoc, assoc, 
                â†assoc ((Î±_ (F.map pâ‚€.p) (F.map (pâ‚.p â‰« pâ‚‚.p)) Aâ‚ƒ.f).hom), Iso.hom_inv_id, 
                id_comp, â†Bicategory.comp_whiskerRight, â†Bicategory.comp_whiskerRight,
                assoc, â†Bicategory.comp_whiskerRight, LaxFunctor.mapComp_assoc_left]
            simp

/- Associator isomorphism part 1 -/
@[simp]
theorem associator_hom_inv_id {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) (pâ‚‚ : Aâ‚‚ âŸ¶ Aâ‚ƒ) : 
    associatorHom F X pâ‚€ pâ‚ pâ‚‚ â‰« associatorInv F X pâ‚€ pâ‚ pâ‚‚ = ðŸ™ ((pâ‚€ â‰« pâ‚) â‰« pâ‚‚) := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = ðŸ™ _
  simp

/- Associator isomorphism part 2 -/
@[simp]
theorem associator_inv_hom_id {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) (pâ‚‚ : Aâ‚‚ âŸ¶ Aâ‚ƒ) : 
    associatorInv F X pâ‚€ pâ‚ pâ‚‚ â‰« associatorHom F X pâ‚€ pâ‚ pâ‚‚ = ðŸ™ (pâ‚€ â‰« pâ‚ â‰« pâ‚‚) := by 
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = ðŸ™ _
  simp

/-- Associator 2-cell.
For a composable triple of 1-cells `(pâ‚€, Î¸â‚€) : (Aâ‚€, fâ‚€) âŸ¶ (Aâ‚, fâ‚), (pâ‚, Î¸â‚) : (Aâ‚, fâ‚) âŸ¶ (Aâ‚‚, fâ‚‚)`,
`(pâ‚‚, Î¸â‚‚) : (Aâ‚‚, fâ‚‚) âŸ¶ (Aâ‚ƒ, fâ‚ƒ)`, the associator `Î±_B` in `B` is the associator in `F â†“ X`: 
`Î±_B : ((pâ‚‚, Î¸â‚‚)(pâ‚, Î¸â‚))(pâ‚€, Î¸â‚€) âŸ¶ (pâ‚‚, Î¸â‚‚)((pâ‚, Î¸â‚)(pâ‚€, Î¸â‚€))` -/
@[simps]
def associator {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) (pâ‚‚ : Aâ‚‚ âŸ¶ Aâ‚ƒ) : 
    (pâ‚€ â‰« pâ‚) â‰« pâ‚‚ â‰… pâ‚€ â‰« pâ‚ â‰« pâ‚‚ where
  hom := associatorHom F X pâ‚€ pâ‚ pâ‚‚
  inv := associatorInv F X pâ‚€ pâ‚ pâ‚‚
  hom_inv_id := associator_hom_inv_id F X pâ‚€ pâ‚ pâ‚‚
  inv_hom_id := associator_inv_hom_id F X pâ‚€ pâ‚ pâ‚‚

@[simp]
theorem comp_whiskerLeft {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) {pâ‚‚ pâ‚ƒ : Aâ‚‚ âŸ¶ Aâ‚ƒ} 
    (Î± : pâ‚‚ âŸ¶ pâ‚ƒ) : whiskerLeft F X (pâ‚€ â‰« pâ‚) Î± =
    (associator F X pâ‚€ pâ‚ pâ‚‚).hom â‰« whiskerLeft F X pâ‚€ (whiskerLeft F X pâ‚ Î±) â‰« 
    (associator F X pâ‚€ pâ‚ pâ‚ƒ).inv := by
  refine Homâ‚‚.ext ?_
  change _ = _ â‰« _ â‰« _
  simp

@[simp]
theorem whiskerRight_comp {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} {pâ‚€ pâ‚ : Aâ‚€ âŸ¶ Aâ‚} (Î± : pâ‚€ âŸ¶ pâ‚) (pâ‚‚ : Aâ‚ âŸ¶ Aâ‚‚) 
    (pâ‚ƒ : Aâ‚‚ âŸ¶ Aâ‚ƒ) : whiskerRight F X Î± (pâ‚‚ â‰« pâ‚ƒ) = 
    (associator F X pâ‚€ pâ‚‚ pâ‚ƒ).inv â‰« whiskerRight F X (whiskerRight F X Î± pâ‚‚) pâ‚ƒ â‰« 
    (associator F X pâ‚ pâ‚‚ pâ‚ƒ).hom := by
  refine Homâ‚‚.ext ?_
  change _ = _ â‰« _ â‰« _
  simp

@[simp]
theorem whisker_assoc {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) {pâ‚ pâ‚‚ : Aâ‚ âŸ¶ Aâ‚‚} (Î± : pâ‚ âŸ¶ pâ‚‚) 
    (pâ‚ƒ : Aâ‚‚ âŸ¶ Aâ‚ƒ) : whiskerRight F X (whiskerLeft F X pâ‚€ Î±) pâ‚ƒ = 
    (associator F X pâ‚€ pâ‚ pâ‚ƒ).hom â‰« whiskerLeft F X pâ‚€ (whiskerRight F X Î± pâ‚ƒ) â‰« 
    (associator F X pâ‚€ pâ‚‚ pâ‚ƒ).inv := by
  refine Homâ‚‚.ext ?_
  change _ = _ â‰« _ â‰« _
  simp

@[simp]
theorem whisker_exchange {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} {pâ‚€ pâ‚ : Aâ‚€ âŸ¶ Aâ‚} {pâ‚‚ pâ‚ƒ : Aâ‚ âŸ¶ Aâ‚‚} (Î±â‚€ : pâ‚€ âŸ¶ pâ‚) 
    (Î±â‚ : pâ‚‚ âŸ¶ pâ‚ƒ) : whiskerLeft F X pâ‚€ Î±â‚ â‰« whiskerRight F X Î±â‚€ pâ‚ƒ = 
    whiskerRight F X Î±â‚€ pâ‚‚ â‰« whiskerLeft F X pâ‚ Î±â‚ := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = _ â‰« _
  simp [Bicategory.whisker_exchange]

@[simp]
theorem pentagon {Aâ‚€ Aâ‚ Aâ‚‚ Aâ‚ƒ Aâ‚„ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) (pâ‚‚ : Aâ‚‚ âŸ¶ Aâ‚ƒ) 
  (pâ‚ƒ : Aâ‚ƒ âŸ¶ Aâ‚„) : whiskerRight F X (associator F X pâ‚€ pâ‚ pâ‚‚).hom pâ‚ƒ â‰« 
  (associator F X pâ‚€ (pâ‚ â‰« pâ‚‚) pâ‚ƒ).hom â‰« whiskerLeft F X pâ‚€ (associator F X pâ‚ pâ‚‚ pâ‚ƒ).hom = 
  (associator F X (pâ‚€ â‰« pâ‚) pâ‚‚ pâ‚ƒ).hom â‰« (associator F X pâ‚€ pâ‚ (pâ‚‚ â‰« pâ‚ƒ)).hom := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ â‰« _ = _ â‰« _
  simp

/- Left unitor forward direction -/
@[simps]
def leftUnitorHom {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : (ðŸ™ Aâ‚€) â‰« p âŸ¶ p where
  Î± := by simpa using (Î»_ p.p).hom
  icc := by simp [â†assoc (F.mapId Aâ‚€.A â–· Aâ‚€.f), â†Bicategory.whisker_exchange, 
                  â†Bicategory.comp_whiskerRight, â†LaxFunctor.mapâ‚‚_leftUnitor_hom]

/- Left unitor reverse direction -/
@[simps]
def leftUnitorInv {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : p âŸ¶ (ðŸ™ Aâ‚€) â‰« p where
  Î± := by simpa using (Î»_ p.p).inv
  icc := by simp [â†assoc (F.mapId Aâ‚€.A â–· Aâ‚€.f), â†Bicategory.whisker_exchange]

/- Left unitor isomorphism part 1 -/
@[simp]
theorem leftUnitor_hom_inv_id {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : 
    leftUnitorHom F X p â‰« leftUnitorInv F X p = ðŸ™ (ðŸ™ Aâ‚€ â‰« p) := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = ðŸ™ _
  simp

/- Left unitor isomorphism part 2 -/
@[simp]
theorem leftUnitor_inv_hom_id {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : 
    leftUnitorInv F X p â‰« leftUnitorHom F X p = ðŸ™ p := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = ðŸ™ _
  simp

/-- Left unitor 2-cell. 
Given a 1-cell `(p, Î¸) : (Aâ‚€, fâ‚€) âŸ¶ (Aâ‚, fâ‚)`, the left unitor `â„“_B` in `B` is the left unitor in 
`F â†“ X`: `â„“_B : (1_{Aâ‚}, r')(p, Î¸) âŸ¶ (p, Î¸)`. -/
@[simps]
def leftUnitor {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : (ðŸ™ Aâ‚€) â‰« p â‰… p where
  hom := leftUnitorHom F X p
  inv := leftUnitorInv F X p
  hom_inv_id := leftUnitor_hom_inv_id F X p
  inv_hom_id := leftUnitor_inv_hom_id F X p

@[simp]
theorem id_whiskerLeft {Aâ‚€ Aâ‚ : F â†“ X} {pâ‚€ pâ‚ : Aâ‚€ âŸ¶ Aâ‚} (Î± : pâ‚€ âŸ¶ pâ‚) : 
    whiskerLeft F X (ðŸ™ Aâ‚€) Î± = (leftUnitor F X pâ‚€).hom â‰« Î± â‰« (leftUnitor F X pâ‚).inv := by
  refine Homâ‚‚.ext ?_
  change _ = _ â‰« _ â‰« _
  simp

/- Right unitor forward direction -/
@[simps]
def rightUnitorHom {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : p â‰« (ðŸ™ Aâ‚) âŸ¶ p where
  Î± := by simpa using (Ï_ p.p).hom
  icc := by simp [â†Bicategory.comp_whiskerRight]
            rw [â†assoc (F.map p.p â— F.mapId Aâ‚.A â–· Aâ‚.f), whisker_assoc_symm, assoc, assoc, assoc,
                â†assoc (Î±_ (F.map p.p) (F.map (ðŸ™ Aâ‚.A)) Aâ‚.f).hom, Iso.hom_inv_id, id_comp, 
                â†Bicategory.comp_whiskerRight, â†LaxFunctor.mapâ‚‚_rightUnitor_hom]
            simp

/- Right unitor reverse direction -/
@[simps]
def rightUnitorInv {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : p âŸ¶ p â‰« (ðŸ™ Aâ‚) where
  Î± := by simpa using (Ï_ p.p).inv
  icc := by simp

/- Right unitor isomorphism part 1 -/
@[simp]
theorem rightUnitor_hom_inv_id {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : 
    rightUnitorHom F X p â‰« rightUnitorInv F X p = ðŸ™ (p â‰« ðŸ™ Aâ‚) := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = ðŸ™ _
  simp

/- Right unitor isomorphism part 2 -/
@[simp]
theorem rightUnitor_inv_hom_id {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : 
    rightUnitorInv F X p â‰« rightUnitorHom F X p = ðŸ™ p := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = ðŸ™ _
  simp

/-- Right unitor 2-cell.
Given a 1-cell `(p, Î¸) : (Aâ‚€, fâ‚€) âŸ¶ (Aâ‚, fâ‚)`, the right unitor `r_B` in `B` is the right unitor in
`F â†“ X`: `r_B : (p, Î¸)(1_{A_0}, r') âŸ¶ (p, Î¸)`. -/
@[simps]
def rightUnitor {Aâ‚€ Aâ‚ : F â†“ X} (p : Aâ‚€ âŸ¶ Aâ‚) : p â‰« (ðŸ™ Aâ‚) â‰… p where
  hom := rightUnitorHom F X p
  inv := rightUnitorInv F X p
  hom_inv_id := rightUnitor_hom_inv_id F X p
  inv_hom_id := rightUnitor_inv_hom_id F X p

@[simp]
theorem whiskerRight_id {Aâ‚€ Aâ‚ : F â†“ X} {pâ‚€ pâ‚ : Aâ‚€ âŸ¶ Aâ‚} (Î± : pâ‚€ âŸ¶ pâ‚) : 
    whiskerRight F X Î± (ðŸ™ Aâ‚) = (rightUnitor F X pâ‚€).hom â‰« Î± â‰« (rightUnitor F X pâ‚).inv := by
  refine Homâ‚‚.ext ?_
  change _ = _ â‰« _ â‰« _
  simp

@[simp]
theorem triangle {Aâ‚€ Aâ‚ Aâ‚‚ : F â†“ X} (pâ‚€ : Aâ‚€ âŸ¶ Aâ‚) (pâ‚ : Aâ‚ âŸ¶ Aâ‚‚) : 
    (associator F X pâ‚€ (ðŸ™ Aâ‚) pâ‚).hom â‰« whiskerLeft F X pâ‚€ (leftUnitor F X pâ‚).hom = 
    whiskerRight F X (rightUnitor F X pâ‚€).hom pâ‚ := by
  refine Homâ‚‚.ext ?_
  change _ â‰« _ = _
  simp


@[simp]
instance : Bicategory (F â†“ X) where
  whiskerLeft pâ‚€ _ _ Î± := whiskerLeft F X pâ‚€ Î±
  whiskerRight pâ‚€ Î± := whiskerRight F X pâ‚€ Î±
  associator pâ‚€ pâ‚ pâ‚‚ := associator F X pâ‚€ pâ‚ pâ‚‚
  leftUnitor p := leftUnitor F X p
  rightUnitor p := rightUnitor F X p
  whiskerLeft_id pâ‚€ pâ‚ := whiskerLeft_id F X pâ‚€ pâ‚
  whiskerLeft_comp p _ _ _ Î±â‚€ Î±â‚ := whiskerLeft_comp F X p Î±â‚€ Î±â‚
  id_whiskerLeft Î± := id_whiskerLeft F X Î±
  comp_whiskerLeft pâ‚€ pâ‚ _ _ Î± := comp_whiskerLeft F X pâ‚€ pâ‚ Î±
  id_whiskerRight pâ‚€ pâ‚ := id_whiskerRight F X pâ‚€ pâ‚
  comp_whiskerRight Î±â‚€ Î±â‚ p := comp_whiskerRight F X Î±â‚€ Î±â‚ p
  whiskerRight_id Î± := whiskerRight_id F X Î±
  whiskerRight_comp Î± pâ‚€ pâ‚ := whiskerRight_comp F X Î± pâ‚€ pâ‚
  whisker_assoc pâ‚€ _ _ Î± pâ‚ := whisker_assoc F X pâ‚€ Î± pâ‚
  whisker_exchange Î±â‚€ Î±â‚ := whisker_exchange F X Î±â‚€ Î±â‚
  pentagon pâ‚€ pâ‚ pâ‚‚ pâ‚ƒ := pentagon F X pâ‚€ pâ‚ pâ‚‚ pâ‚ƒ
  triangle pâ‚€ pâ‚ := triangle F X pâ‚€ pâ‚
